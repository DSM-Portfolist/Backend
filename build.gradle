buildscript {
    ext {
        openfeignVersion = '3.0.3'
        redisVersion = '0.7.2'
        querydslVersion = '4.4.0'
        jwtVersion = '0.9.1'
        junitVersion = '5.6.0'
        assertjVersion = '3.19.0'
    }
}

plugins {
    id 'org.springframework.boot' version '2.5.4'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'jacoco'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}

dependencies {
    // starter
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

    // openfeign
    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: openfeignVersion

    // mail
    implementation 'org.springframework.boot:spring-boot-starter-mail'

    // lombok
    compileOnly 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    // spring security
    implementation 'org.springframework.boot:spring-boot-starter-security'

    // redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation group: 'it.ozimov', name:'embedded-redis', version: redisVersion

    // jwt
    implementation group: 'io.jsonwebtoken', name: 'jjwt', version: jwtVersion

    // validation
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // jpa
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    // querydsl
    implementation "com.querydsl:querydsl-jpa"
    annotationProcessor "com.querydsl:querydsl-apt:$querydslVersion:jpa"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    // database
    runtimeOnly 'mysql:mysql-connector-java'

    // test database
    testImplementation 'com.h2database:h2'

    // test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitVersion
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitVersion
    testImplementation group: 'org.assertj', name: 'assertj-core', version: assertjVersion
}

jacocoTestReport {
    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled false
    }
}

check.dependsOn jacocoTestReport

test {
    useJUnitPlatform()
}
